# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.15.0
pkgrel=3
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(intel-dri libxvmc libpciaccess libdrm xcb-util libxfixes udev)
makedepends=('xorg-server-devel' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'xcb-util')
conflicts=('xorg-server<1.10.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        git-fixes.patch
        kill_mmio_vga_access_helpers.patch
        use_vgahw_functions_instead_of_roll_your_own.patch
        rip_out_PIO_access_functions.patch
        kill_dead_i830_defines.patch)
sha1sums=('78ec39a4470cfc0bf13d269fb915f6c5a498ee62'
          'd7a9102e58c1d6c1bcb99ff6836146bd54a18161'
          '4b85ca8b8beabcd0311f988e0c13249c4cb6be04'
          '41f8196164461f3c1a893159943e49a372faeabb'
          '599cf08a336976c2ca27d36f75270587850049a8'
          'fbdc651c7ede0eeb2218a1a9c89ad71016e79e05')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-fixes.patch"
  echo "Apply kill_dead_i830_defines.patch"
  patch -Np1 -i "${srcdir}/kill_dead_i830_defines.patch"
  echo "Apply rip_out_PIO_access_functions.patch"
  patch -Np1 -i "${srcdir}/rip_out_PIO_access_functions.patch"
  echo "Apply use_vgahw_functions_instead_of_roll_your_own.patch"
  patch -Np1 -i "${srcdir}/use_vgahw_functions_instead_of_roll_your_own.patch"
  echo "Apply kill_mmio_vga_access_helpers.patch"
  patch -Np1 -i "${srcdir}/kill_mmio_vga_access_helpers.patch"

  autoreconf
  ./configure --prefix=/usr --enable-dri
  make
}

package() {
  MAKEFLAGS=-j1
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
